plugins {
    id 'java-library'
    id 'idea'
    id 'net.neoforged.gradle.userdev'
    id 'eclipse'
}

version = "${mc_version}-${build_number}-NEOFORGE"
if (System.getenv().RELEASE_MODE != "1") {
    version += '-SNAPSHOT'
}

repositories {
    mavenLocal()
    maven {
        name = "JEI"
        url "https://maven.blamejared.com/"
        content {
            includeGroup "mezz.jei"
        }
    }
}

runs {
    configureEach {
        workingDirectory project.file('../run')
        systemProperty 'forge.logging.console.level', 'debug'
        systemProperty 'fml.earlyprogresswindow', 'false'

        modSource project.sourceSets.main
    }

    client {
    }
    server {
        programArgument '--nogui'
    }
}

dependencies {
    implementation "net.neoforged:neoforge:21.0.42-beta"
    implementation project(":Xplat")

    compileOnly "mezz.jei:jei-1.21-common-api:19.0.0.11"
    testCompileOnly "mezz.jei:jei-1.21-common-api:19.0.0.11"
}

tasks.named('test').configure {
    //Disable builtin test as we have JUnit enabled in Xplat
    enabled(false)
}

TaskCollection.metaClass.excludingNeoTasks = { ->
    delegate.matching { !it.name.startsWith("neo") }
}

tasks.withType(JavaCompile).excludingNeoTasks().configureEach {
    source(project(":Xplat").sourceSets.main.allSource)
}

tasks.withType(Javadoc).excludingNeoTasks().configureEach {
    source(project(":Xplat").sourceSets.main.allJava)
}

tasks.named("sourcesJar", Jar) {
    from(project(":Xplat").sourceSets.main.allSource)
}

tasks.withType(ProcessResources).excludingNeoTasks().configureEach {
    from project(":Xplat").sourceSets.main.resources
}

jar {
    manifest {
        attributes([
                "Specification-Title": "${mod_name}",
                "Specification-Vendor": "vazkii",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": "${mod_name}",
                "Implementation-Version": "${archiveVersion.get()}",
                "Implementation-Vendor" :"vazkii"
        ])
    }
}

task apiJar(type: Jar, dependsOn: classes) {
    // Sources included because of MinecraftForge/ForgeGradle#369
    from project(":Xplat").sourceSets.main.allJava
    from(sourceSets.main.allJava)
    from(sourceSets.main.output)

    include 'vazkii/patchouli/api/**'

    archiveClassifier = 'api'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            artifact jar
            artifact sourcesJar
            artifact apiJar
        }
    }
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}
